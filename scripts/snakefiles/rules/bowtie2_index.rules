# Set parameters
if not "qsub" in config.keys():
    sys.exit("The parameter qsub must be defined in the config file")

if not "version" in config["genome"].keys():
    sys.exit("Genome version must be defined in the config file")

if not "fasta_file" in config["genome"].keys():
    sys.exit("Fasta file must be defined in the config file")

if not "genome" in config["dir"].keys():
    sys.exit("Genome dir must be defined in the 'dir' section of the config file")

if not "bowtie2" in config.keys():
    config["bowtie2"] = {}

if not "threads" in config["bowtie2"].keys():
    config["bowtie2"]["threads"] = "1"

bowtie2_dir = os.path.join(config["dir"]["genome"], "bowtie2")

# Rule
rule bowtie2_index:
    """Rule for the creation of Bowtie 2 index. Has to be done only once.  The
    output file is used to test whether the index already exists
    when aligning.

    Required parameters:
        config["qsub"]
        config["dir"]["genome"]
        config["genome"]["fasta_file"]

    Optional parameters:
        config["bowtie2"]["threads"]

    Contributors: Claire Rioualen
    """
    input: os.path.join(config["dir"]["genome"], config["genome"]["fasta_file"])
    output: os.path.join(bowtie2_dir, config["genome"]["fasta_file"])
    params:
        threads = config["bowtie2"]["threads"], \
        dir = bowtie2_dir, \
        qsub = config["qsub"] \
          + " -e " + bowtie2_dir + "index_qsub.err" \
          + " -o " + bowtie2_dir + "index_qsub.out"
    log: bowtie2_dir + "/index.log"
    benchmark: bowtie2_dir + "/index_benchmark.txt"
    conda:  "../envs/bowtie2.yaml"
    shell: """
mkdir -p {params.dir}
cp -f {input} {params.dir}/
bowtie2-build {output}  {output} --threads {params.threads} &> {log}
"""

