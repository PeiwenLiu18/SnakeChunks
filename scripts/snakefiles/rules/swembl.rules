# Set parameters
if not "qsub" in config.keys():
    sys.exit("The parameter qsub must be defined in the config file")

if not "swembl" in config.keys():
    config["swembl"] = {}

if not "R" in config["swembl"].keys():
    config["swembl"]["R"] = "0"

if not "N" in config["swembl"].keys():
    config["swembl"]["N"] = "0"

if not "x" in config["swembl"].keys():
    config["swembl"]["x"] = "1"

if not "samples" in config["dir"].keys():
    sys.exit("You must define a location for the sample files.")

if not "peaks" in config["dir"].keys():
    sys.exit("You must define a location for the peak files.")

dir_samples = config["dir"]["samples"]
dir_swembl = os.path.join(config["dir"]["peaks"], "{treatment}_vs_{control}/swembl")

# Rule
rule swembl:
    """
    Peak-calling with SWEMBL.

    Beware: for SWEMBL the peaks MUST be sorted by position, other wise SWEMBL runs indefinitely. 
    Usually by default we sort all bam files by position after alignment. 

    Required parameters:
        config["qsub"]
        config["dir"]["samples"]
        config["dir"]["peaks"]

    Optional parameters:
        config["swembl"]["x"]
        config["swembl"]["R"]
        config["swembl"]["N"]

    Contributors: Claire Rioualen, Lucie Khamvongsa
    """
    input:
        treatment = dir_samples + "/{treatment}/{treatment}{preprocess,.*}.bed",
        control = dir_samples + "/{control}/{control}{preprocess,.*}.bed"
    output:
        peaks_swembl = dir_swembl + "/{treatment}_vs_{control}{preprocess,.*}_swembl.swembl",
        peaks_bed = dir_swembl + "/{treatment}_vs_{control}{preprocess,.*}_swembl.bed",
    params:
        R = config["swembl"]["R"],
        N = config["swembl"]["N"],
        x = config["swembl"]["x"],
        compa_name = "{treatment}_vs_{control}",
        qsub = config["qsub"] \
          + " -e " + dir_swembl + "/{treatment}_vs_{control}{preprocess,.*}_swembl_qsub.err" \
          + " -o " + dir_swembl + "/{treatment}_vs_{control}{preprocess,.*}_swembl_qsub.out"
    log: dir_swembl + "/{treatment}_vs_{control}{preprocess,.*}_swembl.log"
    benchmark: dir_swembl + "/{treatment}_vs_{control}{preprocess,.*}_swembl_benchmark.txt"
    shell: """
(SWEMBL -i {input.treatment} -r {input.control} -B -R {params.R} -N {params.N} -x {params.x} -o {output.peaks_swembl}; \
grep -v '^#' {output.peaks_swembl} \
  | awk -F'\t' '$1 != "Region" || $2 != "Start pos." {{print $1"\t"$2"\t"$3"\t{params.compa_name}_SWEMBL\t"$7"\t+"}}'> {output.peaks_bed}
) &> {log} \
|| (touch {output})
"""

